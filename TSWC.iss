; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; -- Include the Inno Download Plugin header --
#include "idp.iss"

#define MyAppName "Unofficial: Toy Soldiers WarChest - Troubleshooter"
#define MyTinyAppName "Unofficial Toy Soldiers Warchest Troubleshooter"
#define MyAppVersion "1.0"
#define MyAppPublisher "Draythsoft"
#define MyAppURL "http://draythianna.com"
#define MyAppExeName "ToySoldiers.cmd"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2C32D2E9-28CC-4AFC-AA23-3B1F09BEA847}}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\steam\steamapps\common\ToySoldiersWarChest
UsePreviousAppDir=no
DefaultGroupName={#MyTinyAppName}
DisableProgramGroupPage=yes
DisableWelcomePage=no
LicenseFile=C:\Users\HP\Downloads\InnoToySoldiers\embedded\License.txt
InfoBeforeFile=C:\Users\HP\Downloads\InnoToySoldiers\embedded\Preinstall.txt
OutputBaseFilename={#MyTinyAppName}_{#MyAppVersion}
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "app\RunAsDate.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\ViGEmClient.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\toysoldiers_dev.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\ToySoldiersWarChest.lnk"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\ToySoldiers.cmd"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\RunAsDate.chm"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\RunAsDate.cfg"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\readme.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Common
Source: "app\steam_api.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: Custom\SteamOffline Full
Source: "app\steam_api_o.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: Custom\SteamOffline Full
Source: "app\steam_api_o.dll"; DestDir: "{app}"; DestName: "steam_api.dll"; Flags: ignoreversion; Components: FixesOnly
Source: "app\steam_appid.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Custom\SteamOffline Full
Source: "app\steam_interfaces.txt"; DestDir: "{app}"; Flags: ignoreversion; Components: Custom\SteamOffline Full
Source: "app\uplay_r1_loader.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: Custom\UplayOffline Full
Source: "app\uplay_r1_loader_o.dll"; DestDir: "{app}"; Flags: ignoreversion; Components: Custom\UplayOffline Full
Source: "app\uplay_r1_loader_o.dll"; DestDir: "{app}"; DestName: "uplay_r1_loader.dll"; Flags: ignoreversion; Components: FixesOnly

[Icons]
Name: "{group}\{#MyTinyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyTinyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyTinyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyTinyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyTinyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

[Components]
Name: "Full"; Description: "Full Offline Installation"; Types: full custom; Flags: exclusive
Name: "Custom"; Description: "Customize Options"; Types: custom; Flags: exclusive
Name: "Custom\UplayOffline"; Description: "Uplay Emulation"; Types: custom; Flags: dontinheritcheck
Name: "Custom\SteamOffline"; Description: "Steam Emulation"; Types: custom; Flags: dontinheritcheck
Name: "FixesOnly"; Description: "No Emulation"; Types: custom; Flags: exclusive
Name: "Common"; Description: "Game Fixes"; Types: full custom; Flags: fixed

[Types]
Name: "full"; Description: "Full"
Name: "custom"; Description: "Custom"; Flags: iscustom

[Code]
const
  VIGEM_DRIVER_GUID = '{96E42B22-F5E9-42F8-B043-ED0F932F014F}';  // ViGEmBus device class GUID
  VIGEM_URL = 'https://github.com/nefarius/ViGEmBus/releases/download/v1.22.0/ViGEmBus_1.22.0_x64_x86_arm64.exe';
  VIGEM_FILE = 'ViGEmBus_1.22.0_x64_x86_arm64.exe';
  
var
  RegPage: TWizardPage;
  SteamNameEdit, SteamIDEdit, UplayNameEdit, UbisoftIDEdit: TEdit;
  SteamGenBtn, UbisoftGenBtn: TNewButton;
  ViGEmInstaller: String;
  ViGEmNeeded: Boolean;

  // Check if the ViGEmBus driver is already installed by looking in registry
function IsViGEmBusInstalled(): Boolean;
begin
  Result := RegKeyExists(HKLM, 'SYSTEM\CurrentControlSet\Services\ViGEmBus');
end;

function IsDigitString(S: String): Boolean;
var
  I: Integer;
  Ch: Char;
begin
  Result := True;
  for I := 1 to Length(S) do
  begin
    Ch := S[I];
    if (Ch < '0') or (Ch > '9') then
    begin
      Result := False;
      Exit;
    end;
  end;
end;

function IsAlphanumeric(S: String): Boolean;
var
  I: Integer;
  Ch: Char;
begin
  Result := True;
  for I := 1 to Length(S) do
  begin
    Ch := S[I];
    if not (((Ch >= '0') and (Ch <= '9')) or
            ((Ch >= 'A') and (Ch <= 'Z')) or
            ((Ch >= 'a') and (Ch <= 'z'))) then
    begin
      Result := False;
      Exit;
    end;
  end;
end;

function IsValidUUID(S: String): Boolean;
begin
  Result := (Length(S) = 36) and
            (Copy(S, 9, 1) = '-') and
            (Copy(S, 14, 1) = '-') and
            (Copy(S, 19, 1) = '-') and
            (Copy(S, 24, 1) = '-');
end;

function IsValidSteamID64(S: String): Boolean;
begin
  Result := (Length(S) = 17) and
            IsDigitString(S) and
            (Copy(S, 1, 7) = '7656119');
end;


function GenerateRandomSteamID64(): string;
var
  Base, Offset: Int64;
begin
  Base := 76561197960265728;
  Offset := Random(999999999);
  Result := IntToStr(Base + Offset);
end;

function GenerateRandomHexUUID(): String;
var
  I: Integer;
  S, HexChars: String;
begin
//  Randomize;
  HexChars := '0123456789abcdef';
  S := '';

  // 8 hex digits
  for I := 1 to 8 do
    S := S + Copy(HexChars, Random(16) + 1, 1);
  S := S + '-';

  // 4 hex digits
  for I := 1 to 4 do
    S := S + Copy(HexChars, Random(16) + 1, 1);
  S := S + '-';

  // 4 hex digits
  for I := 1 to 4 do
    S := S + Copy(HexChars, Random(16) + 1, 1);
  S := S + '-';

  // 4 hex digits
  for I := 1 to 4 do
    S := S + Copy(HexChars, Random(16) + 1, 1);
  S := S + '-';

  // 12 hex digits
  for I := 1 to 12 do
    S := S + Copy(HexChars, Random(16) + 1, 1);

  Result := S;
end;


procedure SteamGenBtnClick(Sender: TObject);
var
  NewID: string;
begin
  NewID := GenerateRandomSteamID64();
  SteamIDEdit.Text := NewID;
  MsgBox('Generated SteamID64: ' + NewID, mbInformation, MB_OK);
end;

procedure UbisoftGenBtnClick(Sender: TObject);
var
  NewUUID: string;
begin
  NewUUID := GenerateRandomHexUUID();
  UbisoftIDEdit.Text := NewUUID;
  MsgBox('Generated Ubisoft UUID: ' + NewUUID, mbInformation, MB_OK);
end;

function SkipEvent (Sender: TWizardPage): Boolean;
begin
    Result := IsComponentSelected('FixesOnly');
end; 

procedure InitializeWizard;
var
  Label1, Label2, Label3, Label4: TLabel;
begin
  if not IsViGEmBusInstalled() then
  begin
    ViGEmNeeded := True;
    idpAddFile(VIGEM_URL, ExpandConstant('{tmp}\' + VIGEM_FILE));
  end
  else
  begin
    ViGEmNeeded := False;
    MsgBox('ViGEmBus is already installed — skipping download.', mbInformation, MB_OK);
  end;

  RegPage := CreateCustomPage(wpInfoAfter,
    'Registration Information',
    'Please enter your registration details below.');
  RegPage.OnShouldSkipPage := @SkipEvent;
  { Steam Name }
  Label1 := TLabel.Create(WizardForm);
  Label1.Parent := RegPage.Surface;
  Label1.Caption := 'Steam Name:';
  Label1.Top := 16;
  Label1.Left := 0;

  SteamNameEdit := TEdit.Create(WizardForm);
  SteamNameEdit.Parent := RegPage.Surface;
  SteamNameEdit.Top := Label1.Top + 16;
  SteamNameEdit.Left := 0;
  SteamNameEdit.Width := 200;
  SteamNameEdit.Text := 'Player1';

  { SteamID64 }
  Label2 := TLabel.Create(WizardForm);
  Label2.Parent := RegPage.Surface;
  Label2.Caption := 'SteamID64:';
  Label2.Top := SteamNameEdit.Top + 36;
  Label2.Left := 0;

  SteamIDEdit := TEdit.Create(WizardForm);
  SteamIDEdit.Parent := RegPage.Surface;
  SteamIDEdit.Top := Label2.Top + 16;
  SteamIDEdit.Left := 0;
  SteamIDEdit.Width := 180;
  SteamIDEdit.Text := '76561190000000000';

  SteamGenBtn := TNewButton.Create(WizardForm);
  SteamGenBtn.Parent := RegPage.Surface;
  SteamGenBtn.Caption := 'Generate SteamID64';
  SteamGenBtn.Left := SteamIDEdit.Left + SteamIDEdit.Width + 8;
  SteamGenBtn.Top := SteamIDEdit.Top - 1;
  SteamGenBtn.Width := 130;
  SteamGenBtn.OnClick := @SteamGenBtnClick;

  { Uplay Name }
  Label3 := TLabel.Create(WizardForm);
  Label3.Parent := RegPage.Surface;
  Label3.Caption := 'Ubisoft Uplay Name:';
  Label3.Top := SteamIDEdit.Top + 40;
  Label3.Left := 0;

  UplayNameEdit := TEdit.Create(WizardForm);
  UplayNameEdit.Parent := RegPage.Surface;
  UplayNameEdit.Top := Label3.Top + 16;
  UplayNameEdit.Left := 0;
  UplayNameEdit.Width := 200;
  UplayNameEdit.Text := 'Player1';

  { Ubisoft ID }
  Label4 := TLabel.Create(WizardForm);
  Label4.Parent := RegPage.Surface;
  Label4.Caption := 'Ubisoft UUID:';
  Label4.Top := UplayNameEdit.Top + 40;
  Label4.Left := 0;

  UbisoftIDEdit := TEdit.Create(WizardForm);
  UbisoftIDEdit.Parent := RegPage.Surface;
  UbisoftIDEdit.Top := Label4.Top + 16;
  UbisoftIDEdit.Left := 0;
  UbisoftIDEdit.Width := 240;
  UbisoftIDEdit.Text := '00000000-0000-0000-0000-000000000000';

  UbisoftGenBtn := TNewButton.Create(WizardForm);
  UbisoftGenBtn.Parent := RegPage.Surface;
  UbisoftGenBtn.Caption := 'Generate Ubisoft UUID';
  UbisoftGenBtn.Left := UbisoftIDEdit.Left + UbisoftIDEdit.Width + 8;
  UbisoftGenBtn.Top := UbisoftIDEdit.Top - 1;
  UbisoftGenBtn.Width := 150;
  UbisoftGenBtn.OnClick := @UbisoftGenBtnClick;
end;


function NextButtonClick(CurPageID: Integer): Boolean;
var
  SteamID64, UplayName, UbisoftID, SteamName, JSONContent: string;
  begin
  Result := True;
  if CurPageID = RegPage.ID then
  begin
    SteamID64 := Trim(SteamIDEdit.Text);
    SteamName := Trim(SteamNameEdit.Text);
    UplayName := Trim(UplayNameEdit.Text);
    UbisoftID := Trim(UbisoftIDEdit.Text);

    if not IsValidSteamID64(SteamID64) then
    begin
      MsgBox('SteamID64 must be 17 digits and start with 7656119.', mbError, MB_OK);
      Result := False;
      Exit;
    end;

    if not IsAlphanumeric(UplayName) then
    begin
      MsgBox('Uplay name must be alphanumeric.', mbError, MB_OK);
      Result := False;
      Exit;
    end;

    if not IsValidUUID(UbisoftID) then
    begin
      MsgBox('Ubisoft ID must be in the format 00000000-0000-0000-0000-0000000000000.', mbError, MB_OK);
      Result := False;
      Exit;
    end;

    if IsComponentSelected('Custom\SteamOffline') or IsComponentSelected('Full') then
    begin
    SaveStringToFile(ExpandConstant('{userappdata}\Goldberg SteamEmu Saves\settings\account_name.txt'),
      SteamName, False);
    end;
     if IsComponentSelected('Custom\SteamOffline')or IsComponentSelected('Full') then
     begin
    SaveStringToFile(ExpandConstant('{userappdata}\Goldberg SteamEmu Saves\settings\user_steam_id.txt'),
      SteamID64, False);
      end;

    JSONContent :=
      '{' + #13#10 +
      '  "UseDebug": false,' + #13#10 +
      '  "Account": {' + #13#10 +
      '    "AccountId": "' + UbisoftID + '",' + #13#10 +
      '    "Email": "",' + #13#10 +
      '    "Name": "' + UplayName + '",' + #13#10 +
      '    "Password": "",' + #13#10 +
      '    "Country": "en-US",' + #13#10 +
      '    "Ticket": ""' + #13#10 +
      '  },' + #13#10 +
      '  "Save": {' + #13#10 +
      '    "Path": "",' + #13#10 +
      '    "UseAppIdInName": false' + #13#10 +
      '  },' + #13#10 +
      '  "Others": {' + #13#10 +
      '    "ApplicationId": "",' + #13#10 +
      '    "OfflineMode": true' + #13#10 +
      '  },' + #13#10 +
      '  "CDKeys": [],' + #13#10 +
      '  "ChunkInfo": {' + #13#10 +
      '    "ChunkIds": [],' + #13#10 +
      '    "InstalledChunkIds": [],' + #13#10 +
      '    "UseInstalledChunkIds": false' + #13#10 +
      '  },' + #13#10 +
      '  "Achis": [],' + #13#10 +
      '  "Rewards": [],' + #13#10 +
      '  "AvatarsPath": ""' + #13#10 +
      '}';
   if IsComponentSelected('Custom\UplayOffline') or IsComponentSelected('Full') then
   begin
    SaveStringToFile(ExpandConstant('{app}\upc.json'), JSONContent, False);
   end;
  end;
 end;

// Optional: Confirm download completed
procedure idpDownloadAfter2(Sender: TObject; const FileName: String; const ResultCode: Integer);
begin
  if (ResultCode = 0) and (ViGEmNeeded) then
    MsgBox('Downloaded ViGEmBus installer successfully.', mbInformation, MB_OK)
  else if (ResultCode <> 0) and (ViGEmNeeded) then
    MsgBox('Failed to download ViGEmBus installer.', mbError, MB_OK);
end;

procedure CopyTwoFilesAfterInstall();
var
  Source1, Source2, Target1, Target2, TargetDir: string;
begin
  // Define the target directory for copied files
  TargetDir := ExpandConstant('{app}');

  // Create the target directory if needed
  if not DirExists(TargetDir) then
    ForceDirectories(TargetDir);

  // Define full source and destination paths
  Source1 := ExpandConstant('{app}\Game.exe');
  Source2 := ExpandConstant('{app}\GameP.exe');
  Target1 := TargetDir + '\Game_o.exe';
  Target2 := TargetDir + '\Game.exe';

  // Copy first file
  if FileCopy(Source1, Target1, False) then
    Log('Copied: ' + Source1 + ' -> ' + Target1)
  else
    Log('Failed to copy ' + Source1);

  // Copy second file
  if FileCopy(Source2, Target2, False) then
    Log('Copied: ' + Source2 + ' -> ' + Target2)
  else
    Log('Failed to copy ' + Source2);
end;

// Simple file copy helper
function FileCopy2(const SourceFile, TargetFile: String; const Overwrite: Boolean): Boolean;
var
  InStream, OutStream: TFileStream;
begin
  Result := False;
  if (not Overwrite) and FileExists(TargetFile) then Exit;
  try
    InStream := TFileStream.Create(SourceFile, fmOpenRead or fmShareDenyWrite);
    try
      OutStream := TFileStream.Create(TargetFile, fmCreate);
      try
        OutStream.CopyFrom(InStream, InStream.Size);
        Result := True;
      finally
        OutStream.Free;
      end;
    finally
      InStream.Free;
    end;
  except
    Log('Error copying file: ' + SourceFile);
  end;
end;

// Run the ViGEmBus installer during installation if needed
procedure CurStepChanged(CurStep: TSetupStep);
var
  ResultCode: Integer;
begin
  if (CurStep = ssInstall) and (ViGEmNeeded) then
  begin
    ViGEmInstaller := ExpandConstant('{tmp}\' + VIGEM_FILE);
    if FileExists(ViGEmInstaller) then
    begin
      MsgBox('Installing ViGEmBus driver...', mbInformation, MB_OK);
      ShellExec('', ViGEmInstaller, '/quiet /norestart', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
      if ResultCode = 0 then
        MsgBox('ViGEmBus installation complete.', mbInformation, MB_OK)
      else
        MsgBox('ViGEmBus installer returned error code ' + IntToStr(ResultCode), mbError, MB_OK);
    end;
  end;
    if CurStep = ssPostInstall then
    CopyTwoFilesAfterInstall();
end;
